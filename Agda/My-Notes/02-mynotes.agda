{-# OPTIONS --without-K --safe #-}

-- https://www.youtube.com/watch?v=d02hIwhdFTk

-- Type = Set
open import general-notation
open import 01-mynotes hiding (ğŸ˜ ; ğŸ™ ; D)

-- empty type
-- \b0
data ğŸ˜ : Type where

-- HoTT notation: Î  x ê‰ X , A x
-- Agda notation: (x : X) â†’ A x

ğŸ˜-elim : {A : ğŸ˜ â†’ Type} (x : ğŸ˜) â†’ A x
ğŸ˜-elim ()

-- Recall, ğŸ˜ acts like false

-- Negation
-- \neg
Â¬_ : Type â†’ Type
Â¬ A = A â†’ ğŸ˜

infix 1000 Â¬_

ğŸ˜-nondep-elim : {B : Type} â†’ ğŸ˜ â†’ B
ğŸ˜-nondep-elim {B} = ğŸ˜-elim {Î» _ â†’ B}

-- When A â‰¡ Î» _ â†’ B,
-- Î  x : X , A x â‰¡ Î  x : X , B â‰¡ X â†’ B
-- i.e., when A is the constant type family B, the dependent product (function) type is the same as a regular function type

-- Synonym for clarity
is-empty : Type â†’ Type
is-empty A = A â†’ ğŸ˜

-- ğŸ˜-is-empty : Â¬ ğŸ˜
ğŸ˜-is-empty : is-empty ğŸ˜
ğŸ˜-is-empty = ğŸ˜-nondep-elim

ğŸ˜-is-empty' : is-empty ğŸ˜
ğŸ˜-is-empty' = id

ğŸ˜-is-empty'' : is-empty ğŸ˜
ğŸ˜-is-empty'' = Î» ()

-- Unit type, different syntax from previous lecture
-- Record definitions satisfy a certain "Î·" rule
-- Recall, ğŸ™ acts like true
record ğŸ™ : Type where
  constructor
    â‹†

-- To access constructors of a record
open ğŸ™ public

-- ğŸ™-is-nonempty : Â¬ Â¬ ğŸ™
ğŸ™-is-nonempty : Â¬ is-empty ğŸ™
-- ğŸ™-is-nonempty = Î» x â†’ x â‹†
ğŸ™-is-nonempty = Î» x â†’ x â‹†

ğŸ™-is-nonempty' : Â¬ is-empty ğŸ™
ğŸ™-is-nonempty' f = f â‹†


-- Î“ , â‹† : ğŸ™ âŠ¢ P(â‹†) type
-------------------------------------------
-- Î“ âŠ¢ indâ‚ : P(â‹†) â†’ Î  x : ğŸ™ , P(x)
--
-- HoTT: P(â‹†) type
-- Agda: P : ğŸ™ â†’ Type
-- Remember, type families are functions from _ to Type
ğŸ™-elim
  : {P : ğŸ™ â†’ Type}
  â†’ P â‹† -- P(â‹†)
  â†’ (x : ğŸ™)
  â†’ P x -- P(x)
ğŸ™-elim p â‹† = p
-- ğŸ™-elim p x = p -- works when ğŸ™ is defined as record
-- ğŸ™-elim p = Î» â‹† â†’ p

ğŸ™-nondep-elim
  : {P : Type}
  â†’ P
  â†’ ğŸ™ â†’ P
ğŸ™-nondep-elim {P} = ğŸ™-elim {Î» _ â†’ P}

-- Binary digits, isomorphic to Bool
-- \B for bold digits
data ğŸš : Type where
  ğŸ ğŸ : ğŸš

--    Î“ , x : ğŸš âŠ¢ P(x) type    Î“ âŠ¢ a : P(ğŸ)    Î“ âŠ¢ b : P(ğŸ)
--    --------------------------------------------------------------------------
--    Î“ , x : ğŸš âŠ¢ indâ‚‚ (a , b , x) : P(x)
--
-- This is like the dependent if[]_then_else from lecture 1
ğŸš-elim
  : {P : ğŸš â†’ Type}
  â†’ P ğŸ
  â†’ P ğŸ
  â†’ (x : ğŸš)
  â†’ P x
ğŸš-elim pâ‚€ pâ‚ ğŸ = pâ‚€
ğŸš-elim pâ‚€ pâ‚ ğŸ = pâ‚

-- This is like the non-dependent if_then_else from lecture 1
ğŸš-nondep-elim
  : {P : Type}
  â†’ P
  â†’ P
  â†’ ğŸš
  â†’ P
ğŸš-nondep-elim {P} = ğŸš-elim {Î» _ â†’ P}

-- Eliminators are important to understand what type theory is, but they're inconvenient to use in practice. So in Agda, instead of using eliminators, we use pattern matching. It is proven that definitions by pattern matching are equivalent to definitions using eliminators (provided we are using --without-K).

-- Î  types in Agda are primitive.
-- 
-- The HoTT type Î  x : X , A x is written in Agda as
-- 
-- 		(x : X) â†’ A x			or
-- 		âˆ€ (x : X) â†’ A x		or
-- 		âˆ€ x â†’ A x				(if Agda can infer X).
--
-- We can introduce Î -syntax if we wish:

Pi : (A : Type) (B : A â†’ Type) â†’ Type
Pi A B = (x : A) â†’ B x
-- For special colon, type "\:4"
syntax Pi A (Î» x â†’ b) = Î  x ê‰ A , b

-- Function composition

-- Private in a useless module because this definition is just for learning. This is the normal composition we're used to seeing.
module _ where
  private
    _âˆ˜_ : {A B C : Type} â†’ (B â†’ C) â†’ (A â†’ B) â†’ (A â†’ C)
    (g âˆ˜ f) x = g (f x)

-- Function composition should be dependent (more general)
_âˆ˜_
  : {A B : Type} {C : B â†’ Type}
  â†’ (Î  y ê‰ B , C y)
  â†’ (f : A â†’ B)
  â†’ Î  x ê‰ A , C (f x)
(g âˆ˜ f) x = g (f x)

-- The types-as-mathematical-statements (or types-as-proofs) reading of dependent function composition is:
-- If (for all y : B we have that C y) and f : A â†’ B is any function, then for all x : A we have that C (f x).

-- Î£-types:

module _ where
  private

    data Î£ {A : Type} (B : A â†’ Type) : Type where
      _,_ : (x : A) (y : B x) â†’ Î£ {A} B -- Curly brackets means A can be inferred

    -- prâ‚ : {A : Type} {B : A â†’ Type} â†’ Î£ {A} B â†’ A
    prâ‚ : {A : Type} {B : A â†’ Type} â†’ Î£ B â†’ A
    prâ‚ (x , y) = x

    prâ‚‚ : {A : Type} {B : A â†’ Type} â†’ (z : Î£ B) â†’ B (prâ‚ z)
    prâ‚‚ (x , y) = y

-- Our preferred definition:
record Î£ {A : Type} (B : A â†’ Type) : Type where
  constructor
    _,_
  field
    prâ‚ : A
    prâ‚‚ : B prâ‚
-- This satisfies the Î·-rule z = (prâ‚ z , prâ‚‚ z), which the previous definition using "data" doesn't.

open Î£ public
infixr 0 _,_

-- prâ‚' : {A : Type} {B : A â†’ Type} â†’ Î£ {A} B â†’ A
prâ‚' : {A : Type} {B : A â†’ Type} â†’ Î£ B â†’ A
prâ‚' = prâ‚

-- prâ‚‚' : {A : Type} {B : A â†’ Type} â†’ (z : Î£ B) â†’ B (prâ‚ z)
prâ‚‚' : {A : Type} {B : A â†’ Type} â†’ ((x , y) : Î£ B) â†’ B x -- pattern matching works because of Î·-rule
prâ‚‚' = prâ‚‚

Sigma : (A : Type) (B : A â†’ Type) â†’ Type
Sigma A B = Î£ {A} B

-- \:4
syntax Sigma A (Î» x â†’ b) = Î£ x ê‰ A , b
infix -1 Sigma

-- Recall that we defined D as follows in lecture 1
-- In Agda, dependent types are simply functions
D : Bool â†’ Type
D true = â„•
D false = Bool

-- Examples
Î£-exampleâ‚ Î£-exampleâ‚‚ : Î£ b ê‰ Bool , D b
Î£-exampleâ‚ = (true , 17)
Î£-exampleâ‚‚ = (false , true)

-- Î£-elim is "uncurry":
Î£-elim
  : {A : Type} {B : A â†’ Type} {C : (Î£ x ê‰ A , B x) â†’ Type}
  â†’ ((x : A) (y : B x) â†’ C (x , y))
  â†’ (z : (Î£ x ê‰ A , B x)) â†’ C z -- *
-- *Remember, for any elimination rule, we want to prove that for any element (z) of the new type (Î£), some property holds (C).
Î£-elim f (x , y) = f x y

Î£-curry
  : {A : Type} {B : A â†’ Type} {C : (Î£ x ê‰ A , B x) â†’ Type}
  â†’ ((z : (Î£ x ê‰ A , B x)) â†’ C z)
  â†’ (x : A) (y : B x) â†’ C (x , y)
Î£-curry f x y = f (x , y)

-- non-dependent pair, AKA cartesian product
_Ã—_ : Type â†’ Type â†’ Type
A Ã— B = Î£ x ê‰ A , B

infixr 2 _Ã—_

-- Binary products are special cases of products.
-- We will have that Aâ‚€ Ã— Aâ‚ â‰… Î  (n ê‰ 2) , A n â‰… ((n : ğŸš) â†’ A n)
--   where
--     A ğŸ = Aâ‚€
--     A ğŸ = Aâ‚
--     A : ğŸš â†’ Type
--
-- f â†¦ (f ğŸ , f ğŸ)
-- (aâ‚€ , aâ‚) â†¦ ğŸš-elim aâ‚€ aâ‚
-- But we need function extensionality to prove that this works.

-- Various uses of Î£:
--
-- 

-- Binary sums
-- \.+
data _âˆ”_ (A B : Type) : Type where
  inl : A â†’ A âˆ” B
  inr : B â†’ A âˆ” B

-- Mathematically A âˆ” B is (disjoint) union.
-- Logically, it is "or" (disjunction).

infixr 20 _âˆ”_

âˆ”-elim
  : {A B : Type} (C : A âˆ” B â†’ Type)
  â†’ ((x : A) â†’ C (inl x)) -- f
  â†’ ((y : B) â†’ C (inr y)) -- g
  â†’ (z : A âˆ” B) â†’ C z
âˆ”-elim C f g (inl x) = f x
âˆ”-elim C f g (inr y) = g y

-- "If (A implies C) and (B implies C), then (A or B implies C)."
âˆ”-nondep-elim
  : {A B C : Type}
  â†’ (A â†’ C)
  â†’ (B â†’ C)
  â†’ (A âˆ” B â†’ C)
âˆ”-nondep-elim {A} {B} {C} = âˆ”-elim (Î» z â†’ C)

-- Binary sums are special cases of sums.
-- We will have that Aâ‚€ âˆ” Aâ‚ â‰… Î£ (n : ğŸš) , A n
--   where
--     A ğŸ = Aâ‚€
--     A ğŸ = Aâ‚
--
-- inl aâ‚€ â†¦ (ğŸ , aâ‚€)
-- inr aâ‚ â†¦ (ğŸ , aâ‚)

-- The identity type.
--
-- We call an element of the identity type x â‰¡ y an "identification" or a "path".
-- This is a type family instead of a simple type.
data _â‰¡_ {A : Type} : A â†’ A â†’ Type where
  refl : (x : A) â†’ x â‰¡ x

-- refl x is a proof that x is equal to itself.

infix 0 _â‰¡_

-- The following is also called "J"
â‰¡-elim
  : {X : Type} (A : (x y : X) â†’ x â‰¡ y â†’ Type)
  â†’ ((x : X) â†’ A x x (refl x)) -- f
  â†’ (x y : X) (p : x â‰¡ y) â†’ A x y p
â‰¡-elim A f x x (refl x) = f x

-- To conclude that a property A x y p of identifications p of elements x and y holds for all x, y, and p, it is enough to show that A x x (refl x) holds for all x.
-- The reason this works is confusing and mysterious, but that's ok.

â‰¡-nondep-elim
  : {X : Type} (A : X â†’ X â†’ Type)
  â†’ ((x : X) â†’ A x x)
  â†’ (x y : X) â†’ x â‰¡ y â†’ A x y
â‰¡-nondep-elim A = â‰¡-elim (Î» x y _ â†’ A x y)

-- Transitivity of â‰¡
trans : {A : Type} {x y z : A} â†’ x â‰¡ y â†’ y â‰¡ z â†’ x â‰¡ z
trans p (refl y) = p

-- Symmetry of â‰¡
sym : {A : Type} {x y : A} â†’ x â‰¡ y â†’ y â‰¡ x
sym (refl x) = refl x

ap : {A B : Type} (f : A â†’ B) {x y : A} â†’ x â‰¡ y â†’ f x â‰¡ f y
ap f (refl x) = refl (f x)

apâ‚‚
  : {A B C : Type} (f : A â†’ B â†’ C) {x x' : A} {y y' : B}
  â†’ x â‰¡ x'
  â†’ y â‰¡ y'
  â†’ f x y â‰¡ f x' y'
apâ‚‚ f (refl x) (refl y) = refl (f x y)
-- apâ‚‚ f (refl x) y = ap (f x) y

-- For all x and y, if x and y are equal, then any property that holds for x also holds for y. Part of Leibniz principle.
transport
  : {X : Type} (A : X â†’ Type)
  â†’ {x y : X}
  â†’ x â‰¡ y
  â†’ A x â†’ A y
transport A (refl x) a = a

_âˆ™_
  : {A : Type} {x y z : A}
  â†’ x â‰¡ y
  â†’ y â‰¡ z
  â†’ x â‰¡ z
-- refl x âˆ™ refl y = refl x
_âˆ™_ = trans

infixl 7 _âˆ™_

_â»Â¹ : {A : Type} {x y : A} â†’ x â‰¡ y â†’ y â‰¡ x
_â»Â¹ = sym

infix 40 _â»Â¹

_â‰¤_ : â„• â†’ â„• â†’ Type
0 â‰¤ y = ğŸ™
suc x â‰¤ 0 = ğŸ˜
suc x â‰¤ suc y = x â‰¤ y

_â‰¥_ : â„• â†’ â„• â†’ Type
x â‰¥ y = y â‰¤ x

_*_ : â„• â†’ â„• â†’ â„•
0 * y = 0
suc x * y = (x * y) + y

infixr 30 _*_

-- The following are statements of theorems. Not proofs.
-- A type is a statement of a theorem. An element (or "inhabitant") of a type is a proof of a theorem.

-- An important idea here is that logic is not separate from mathematics. All of logic is contained inside these mathematical ideas (Î£, Î , etc.).

_divides_ : â„• â†’ â„• â†’ Type
x divides y = Î£ z ê‰ â„• , x * z â‰¡ y

is-prime : â„• â†’ Type
is-prime p = (p â‰¥ 2) Ã— (((x y : â„•) â†’ (x * y) â‰¡ p â†’ (x â‰¡ 1) âˆ” (x â‰¡ p)))

twin-prime-conjecture : Type
twin-prime-conjecture =
  (n : â„•)
  â†’ Î£ p ê‰ â„•
    , (p â‰¥ n)
    Ã— is-prime p
    Ã— is-prime (p + 2)

-- A proof of the twin prime conjecture would look like:
--   some-proof : twin-prime-conjecture
--   some-proof = ?

there-are-infinitely-many-primes : Type
there-are-infinitely-many-primes =
  (n : â„•)
  â†’ Î£ p ê‰ â„• , (p â‰¥ n) Ã— is-prime p
